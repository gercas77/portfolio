resource "aws_vpc" "main" {
    cidr_block           = var.vpc_cidr
    enable_dns_support   = true
    enable_dns_hostnames = true

    tags = { Name = "${var.name_prefix}-vpc" }
}

resource "aws_internet_gateway" "main" {
    vpc_id = aws_vpc.main.id

    tags = { Name = "${var.name_prefix}-igw" }
}

# --- Public subnets (ALB + ECS instances) ---

resource "aws_subnet" "public" {
    count = length(var.availability_zones)

    vpc_id                  = aws_vpc.main.id
    cidr_block              = cidrsubnet(var.vpc_cidr, 8, count.index)
    availability_zone       = var.availability_zones[count.index]
    map_public_ip_on_launch = true

    tags = { Name = "${var.name_prefix}-public-${var.availability_zones[count.index]}" }
}

resource "aws_route_table" "public" {
    vpc_id = aws_vpc.main.id

    route {
        cidr_block = "0.0.0.0/0"
        gateway_id = aws_internet_gateway.main.id
    }

    tags = { Name = "${var.name_prefix}-public-rt" }
}

resource "aws_route_table_association" "public" {
    count = length(aws_subnet.public)

    subnet_id      = aws_subnet.public[count.index].id
    route_table_id = aws_route_table.public.id
}

# --- Security groups ---

resource "aws_security_group" "alb" {
    name_prefix = "${var.name_prefix}-alb-"
    vpc_id      = aws_vpc.main.id
    description = "ALB - allow HTTP/HTTPS inbound"

    ingress {
        description = "HTTP"
        from_port   = 80
        to_port     = 80
        protocol    = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    ingress {
        description = "HTTPS"
        from_port   = 443
        to_port     = 443
        protocol    = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    egress {
        from_port   = 0
        to_port     = 0
        protocol    = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    lifecycle { create_before_destroy = true }

    tags = { Name = "${var.name_prefix}-alb-sg" }
}

resource "aws_security_group" "ecs" {
    name_prefix = "${var.name_prefix}-ecs-"
    vpc_id      = aws_vpc.main.id
    description = "ECS instances - allow traffic from ALB only"

    ingress {
        description     = "From ALB"
        from_port       = 0
        to_port         = 65535
        protocol        = "tcp"
        security_groups = [aws_security_group.alb.id]
    }

    egress {
        from_port   = 0
        to_port     = 0
        protocol    = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    lifecycle { create_before_destroy = true }

    tags = { Name = "${var.name_prefix}-ecs-sg" }
}
